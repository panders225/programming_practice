/************************************************************************
 * Last Run Date: 08 April 2017	            							*
 * Program Name: panders2_hw10_prog.sas									*
 * Program Location: C:/Users/Philip/Schools/TAMU/STAT_657/				*
 *				SASUniversityEdition/myfolders/homework/git_hw/hw_10/sasprogs/	* 
 * Creation Date: 01 April 2017											*
 * Author: Philip Anderson												*
 * Purpose: Meeting objectives for Assignment 10						*
 * Inputs: orion folder         *
 * Output: panders2_hw10_output.pdf															*
 * Modification History: *
 * Execution Instructions: N/A - run as-is												*
*/**********************************************************************;

/* 
Question 3 - set a system option
 that writes macro variables to the log as they are resolved, and another option
 that writes to the SAS log the text that is generated by macro execution
*/
options symbolgen mprint;

/* macro below is from question 5 */
%let hw_num = 10;

/* Question 4 - use the appropriate option to set up your SAS session to find
	the previously-specified autocall macro
 */
options mautosource sasautos='/folders/myfolders/macros';


/* 
Question 5 - Set up an "A&M" folder at the same level as "macros"
create a fileref path to it, and make the hw number a macro variable
use a function to mask the macro functionality of the ampersand in A&M
*/

filename pdf_out "/folders/myfolders/%nrstr(A&M)/panders2_hw&hw_num._output.pdf";

ods pdf file=pdf_out;

/* Question 6 - test the autocall library */
/* leaving this commented out - cannot get this to run - seems to be an issue
with the SASUniversityEdition installation.  messsage should be:
"This macro causes all of the Color Utility macros to be made available.

If %colormac is called without any parameters, a message is displayed
 on the log that the Color Utility
 ...
 no messages are displayed"
*/
/*%colormac(help);*/


/* Question 7 - call your donate() macro stored in the autocall library*/
libname orion '/folders/myfolders/orion_dat/sasdata' access=readonly;

title 'Donations of Female Employees Hired between 01Jan1996 and 31Dec2005' ;
%donate(work, female, 01Jan1996, 31Dec2005);
title;

/* Use the catalog procedure to list macros that are stored in the WORK library */

proc catalog cat=work.sasmac1;
	contents;
	title 'Compiled Macros in the Work Library';
run;


/* Question 9 - use a data step to create a new table called salary_f9605*/

data salary_f9605;
set female1996 (keep=employee_id employee_name salary) end=eof;

	/*Part A: initialize two macro variables upon reading the first record*/
	/* use _n_=1 to determine the first row */
	if _n_ = 1 then do;
		call symputx('emp_id', employee_id);
		call symputx('sal', salary);	
	end;
	
	/* 
	 * Part B: create a series of macro variables whose names end with the
	 * Employee ID numbers.  Reformat the corresponding name value .
	 */
	call symputx('NAME'||left(employee_id)
			, compress(scan(employee_name, 2))||''||compress(scan(employee_name, 1)));
	
	/* Part A continued - for each subsequent row, compare the salary with the */
	/* value stored in the macro variable using symget() and replace it if higher */
		if salary > symget('sal') then do;
			call symputx('emp_id', employee_id);
			call symputx('sal', salary);
		end;
	
run;


/* Reformat the salary macro variable created above to include dollars*/
data _null_;
	call symputx('sal_final', put(&sal. , dollar8.));
run;


/* Question 10 - use the print procedure to print the data portion of the salary_f9605 */
/* Use macro variables to automatically populate the title */
title1 "Salary Analysis of Selected Employees";
title2 "Top Salary = &sal_final to: &&NAME&emp_id.";
proc print data=salary_f9605; 
run;

ods pdf close;


title;
footnote;

/*
 * Answer to Question 8
 * By default, any defined macro used in a SAS program is 
 * an automatic entry into a SAS catalog (SASMAC1) in the work library
 */